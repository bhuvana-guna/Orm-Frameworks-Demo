buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.ebean:ebean:11.22.10'
        classpath 'io.ebean:ebean-agent:11.11.1'
    }
}

import io.ebean.enhance.Transformer;
import io.ebean.enhance.ant.OfflineFileTransform;
import io.ebean.enhance.ant.TransformationListener;

// Call this task from your IDE after project compilation.
task enhanceEntities << {
    String classDir = project.sourceSets.main.output.classesDir
    String packageToScan = 'com.github.vkuzel.orm_frameworks_demo.ebean.domain'

    ClassLoader classLoader = this.class.getClassLoader()
    TransformationListener transformationListener = new TransformationListener() {
        void logEvent(String msg) {
            project.logger.info(msg)
        }

        void logError(String msg) {
            project.logger.error(msg)
        }
    }

    Transformer transformer = new Transformer(classLoader, '')
    OfflineFileTransform offlineFileTransform = new OfflineFileTransform(transformer, classLoader, classDir)
    offlineFileTransform.setListener(transformationListener)
    offlineFileTransform.process(packageToScan)
}

classes.doLast {
    enhanceEntities.execute()
}
